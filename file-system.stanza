;; See license.txt for details about licensing.

defpackage file-system :
  import core
  import collections
  import math

public defn split-pathname (pathname:String, c:Char) -> [String, String]:
  val index = last-index-of-char(pathname, c)
  match(index) :
    (f:False) : ["", pathname]
    (i:Int)   : [pathname[0 to (i + 1)], pathname[(i + 1) to false]]

public defn dir-name (pathname:String, default:String) -> String :
  val [dir, base] = split-pathname(pathname, '/')
  default when dir == "" else dir

public defn dir-name (pathname:String) : dir-name(pathname, ".")

public defn base-name (pathname:String) -> String :
  split-pathname(pathname, '/')[1]

public defn prefix-name (pathname:String) -> String :
  split-pathname(pathname, '.')[0]

public defn suffix-name (pathname:String) -> String :
  split-pathname(pathname, '.')[1]

public val MODE-U-R = 1 << 6
public val MODE-U-W = 1 << 7
public val MODE-U-X = 1 << 8
public val MODE-G-R = 1 << 3
public val MODE-G-W = 1 << 4
public val MODE-G-X = 1 << 5
public val MODE-O-R = 1 << 0
public val MODE-O-W = 1 << 1
public val MODE-O-X = 1 << 2

extern malloc: (long) -> ptr<byte>
extern free: (ptr<byte>) -> int
extern mkdir: (ptr<byte>, int) -> int
extern getcwd: (ptr<byte>, int) -> ptr<byte>
extern chdir: (ptr<byte>) -> int
extern printf: (ptr<byte>, ? ...) -> int
extern opendir: (ptr<byte>) -> ptr<?>
extern readdirent: (ptr<?>, ptr<?>) -> int
extern closedir: (ptr<?>) -> int

public lostanza defn mkdir (dir:ref<String>, mode:ref<Int>) -> ref<Int> :
  val res = call-c mkdir(addr!(dir.chars), mode.value)
  return new Int{res}

lostanza deftype Dir :
  value: ptr<?>

lostanza deftype DirEntry :
  file-number:int
  record-length:int
  type:byte
  name-length:byte
  name:ptr<byte>

lostanza defn DirEntry () -> ref<?> :
  return new DirEntry{0, 0, 0 as byte, 0 as byte, 0L as ptr<byte>}

lostanza defn open? (dir:ref<Dir>) -> ref<True|False> :
  if (dir.value as long) == 0L :
    return false
  else :
    return true

lostanza defn opendir (pathname:ref<String>) -> ref<Dir> :
  val ptr = call-c opendir(addr!(pathname.chars))
  return new Dir{ptr}

public defn directory? (pathname:String) -> True|False :
  open?(opendir(pathname))

lostanza defn closedir (dir:ref<Dir>) -> ref<False> :
  if open?(dir) == true :
    call-c closedir(dir.value)
  return false

lostanza defn readdir (dir:ref<Dir>, dirent:ref<DirEntry>) -> ref<True|False> :
  if open?(dir) == true :
    val res = call-c readdirent(dir.value, addr!([dirent]))
    if res == 0L :
      return true
    else :
      return false
  else :
    return false

public lostanza defn get-cwd () -> ref<String> :
  val bytes = call-c malloc(256)
  val res   = call-c getcwd(bytes, 256)
  return String(res)

public lostanza defn ch-dir (dirname:ref<String>) -> ref<True|False> :
  val res = call-c chdir(addr!(dirname.chars))
  if res == 0L :
    return true
  else :
    return false

lostanza defn entry-name (ent:ref<DirEntry>) -> ref<String> :
  return String(ent.name)

lostanza defn entry-dir? (ent:ref<DirEntry>) -> ref<True|False> :
  if ent.type == 4L :
    return true
  else :
    return false

public defn directory-entries (pathname:String) -> Seq<String> :
  generate<String> :
    val ent = DirEntry()
    defn* loop (dir) :
      val closed? = readdir(dir, ent)
      if closed? :
        closedir(dir)
      else :
        yield(string-join([entry-name(ent) ("/" when entry-dir?(ent) else "")]))
        loop(dir)
    loop(opendir(pathname))
